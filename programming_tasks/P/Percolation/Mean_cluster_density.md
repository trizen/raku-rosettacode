[1]: https://rosettacode.org/wiki/Percolation/Mean_cluster_density

# [Percolation/Mean cluster density][1]

```raku
my @perc;
my $fill = 'x';
Â 
enum Direction <DeadEnd Up Right Down Left>;
Â 
my $ğ˜’ = perctest(15);
.fmt("%-2s").say for @perc;
say "ğ˜± = 0.5, ğ˜• = 15, ğ˜’ = $ğ˜’\n";
Â 
my $trials = 5;
for 10, 30, 100, 300, 1000 -> $ğ˜• {
    my $ğ˜’ = ( [+] perctest($ğ˜•) xx $trials ) / $trials;
    say "ğ˜± = 0.5, trials = $trials, ğ˜• = $ğ˜•, ğ˜’ = $ğ˜’";
}
Â 
sub infix:<deq> ( $a, $b ) { $a.defined && ($a eq $b) }
Â 
sub perctest ( $grid ) {
    generate $grid;
    my $block = 1;
    for ^$grid X ^$grid -> ($y, $x) {
        fill( [$x, $y], $block++ ) if @perc[$y; $x] eq $fill
    }
    ($block - 1) / $gridÂ²;
}
Â 
sub generate ( $grid ) {
    @perc = ();
    @perc.push: [ ( rand < .5 ?? '.' !! $fill ) xx $grid ] for ^$grid;
}
Â 
sub fill ( @cur, $block ) {
    @perc[@cur[1]; @cur[0]] = $block;
    my @stack;
    my $current = @cur;
Â 
    loop {
        if my $dir = direction( $current ) {
            @stack.push: $current;
            $current = move $dir, $current, $block
        }
        else {
            return unless @stack;
            $current = @stack.pop
        }
    }
Â 
    sub direction( [$x, $y] ) {
        ( Down  if @perc[$y + 1][$x] deq $fill ) ||
        ( Left  if @perc[$y][$x - 1] deq $fill ) ||
        ( Right if @perc[$y][$x + 1] deq $fill ) ||
        ( Up    if @perc[$y - 1][$x] deq $fill ) ||
        DeadEnd
    }
Â 
    sub move ( $dir, @cur, $block ) {
        my ( $x, $y ) = @cur;
        given $dir {
            when Up    { @perc[--$y; $x] = $block }
            when Down  { @perc[++$y; $x] = $block }
            when Left  { @perc[$y; --$x] = $block }
            when Right { @perc[$y; ++$x] = $block }
        }
        [$x, $y]
    }
}
Â 
```

#### Output:
```
.  .  1  .  2  .  .  3  .  .  .  4  .  .  . 
2  2  .  .  2  2  2  .  5  5  .  4  4  4  4 
2  2  2  2  2  .  2  .  5  .  .  4  .  .  4 
2  .  2  .  2  2  .  .  .  .  4  4  4  .  4 
.  .  .  .  .  2  .  .  .  .  4  4  4  .  . 
6  6  6  6  .  .  7  7  .  .  4  .  4  4  . 
6  .  6  .  .  .  .  .  4  4  4  4  4  .  . 
6  6  6  .  .  .  8  8  .  4  4  4  .  .  4 
6  .  .  .  9  .  .  .  .  .  .  4  4  .  4 
.  10 .  11 .  .  12 12 .  4  .  .  4  4  4 
11 .  11 11 11 11 .  12 .  4  .  4  4  4  . 
11 11 11 11 11 11 11 .  .  4  4  .  .  4  . 
11 11 11 11 .  11 .  .  .  4  4  4  4  4  . 
11 11 11 .  11 11 11 .  .  4  4  4  4  .  13
.  11 11 .  11 11 .  .  .  .  4  4  .  14 . 
ğ˜± = 0.5, ğ˜• = 15, ğ˜’ = 0.062222

ğ˜± = 0.5, trials = 5, ğ˜• = 10, ğ˜’ = 0.114
ğ˜± = 0.5, trials = 5, ğ˜• = 30, ğ˜’ = 0.082444
ğ˜± = 0.5, trials = 5, ğ˜• = 100, ğ˜’ = 0.06862
ğ˜± = 0.5, trials = 5, ğ˜• = 300, ğ˜’ = 0.066889
ğ˜± = 0.5, trials = 5, ğ˜• = 1000, ğ˜’ = 0.0659358
```